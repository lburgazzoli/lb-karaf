
// *****************************************************************************
// -Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //classpath 'be.jlr-home.gradle:scrPlugin:0.1.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'osgi'
    //apply plugin: 'scr'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    version             = '3.0.3-SNAPSHOT'
    group               = 'com.github.lburgazzoli'

    project.ext {
        versions = [
            slf4j               : '1.7.12',
            log4j2              : '2.3',
            felixConfigAdmin    : '1.8.2',
            felixScr            : '1.8.2',
            felixScrAnnotations : '1.9.10',
            karaf               : '3.0.3',
            guava               : '18.0',
            commonsLang         : '3.4',
            commonsCollections  : '4.0',
            osgi                : '5.0.0',
            lbEtcd              : '1.0.0-SNAPSHOT'
        ]

        if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            nexusUsername = sonatypeUsername
            nexusPassword = sonatypePassword
        } else {
            nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
            nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
        }

        gradleVersion       = '2.3'
        isReleaseVersion    = !version.endsWith("SNAPSHOT")
        gitRoot             = "https://github.com/lburgazzoli"
        gitProject          = "https://github.com/lburgazzoli/lb-karaf"
        deploySnapshot      = "https://oss.sonatype.org/content/repositories/snapshots"
        gradleScriptDir     = "${rootProject.projectDir}/gradle"
        osgiVersion         = project.version.replaceAll('-','.')
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
        compile("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }

        runtime "org.apache.logging.log4j:log4j-api:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-core:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jcl:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jul:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-slf4j-impl:$versions.log4j2"

        compile "com.google.guava:guava:$versions.guava"
        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "org.apache.felix:org.apache.felix.configadmin:$versions.felixConfigAdmin"
        //compile "org.apache.felix:org.apache.felix.scr.annotations:$versions.felixScrAnnotations"
        compile "org.apache.karaf.shell:org.apache.karaf.shell.console:$versions.karaf"
        compile "org.apache.karaf.shell:org.apache.karaf.shell.table:$versions.karaf"
        compile "org.osgi:org.osgi.core:$versions.osgi"
        compile "org.osgi:org.osgi.compendium:$versions.osgi"
    }

    jar {
        baseName = "${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = osgiVersion
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.name}-${osgiVersion}"
        manifest.attributes['Implementation-Version' ] = osgiVersion
        manifest.attributes['Implementation-Vendor'  ] = 'lb'
    }

    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        sign    = false
    }

    extraArchive {
        javadoc = true
        sources = true
        tests   = false
    }

    modifyPom {
        project {
            name          = "${project.name}"
            description   = "${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
    
    // *************************************************************************
    //
    // *************************************************************************

    dependencyUpdates {
        revision = 'release'
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

